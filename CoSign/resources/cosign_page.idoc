<html>
 <head>
  <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
  <title>CoSign htmlIncludeOrString
 </head>
 <body>
[[% Core functionality extensions %]]
<@ dynamichtml extra_setup_search_results_action_popups @>
	<$ traceLog = '###begin extra_setup_search_results_action_popups log' $>
	<$ include super.extra_setup_search_results_action_popups $>

	<$ if enableSignOnSearch $>
		<$ traceLog = traceLog & '\n\tEnable Sign on Search: ' & enableSignOnSearch $>
		<$ if isTrue( enableSignOnSearch ) $>
			<$ include cosign_compute_userhasrequiredrole $>
			<$ exec rsAppendNewRow( "PopupProps" ) $>
			<$ exec setValue( "PopupProps", "label", lc( "wwSignWithCoSign" ) ) $>
			<$ exec setValue( "PopupProps", "function", "<$ SearchHttpCgiPath $>?IdcService=COSIGN_SIGNDOCUMENT&dDocName=<$ url( dDocName ) $>&dID=<$ url( dID ) $><$ include idc_token_url_appending_parameter $>" ) $>
			<$ exec setValue( "PopupProps", "ifClause", "isTrue( userHasRequiredRole ) AND userHasRole( 'CoSignSigner' ) AND ( NOT strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), '' ) AND NOT strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), 'Externally Signed' ) )" ) $>
			<$ exec setValue( "PopupProps", "linkTarget", "" ) $>
			<$ exec setValue( "PopupProps", "class", "document" ) $>
			<$ exec setValue( "PopupProps", "id", "signWithCoSign" ) $>
		<$ endif $>
	<$ endif $>

	<$ exec rsAppendNewRow( "PopupProps" ) $>
	<$ exec setValue( "PopupProps", "label", lc( "wwReviewSignatures" ) ) $>
	<$ exec setValue( "PopupProps", "function", "<$ SearchHttpCgiPath $>?IdcService=COSIGN_VERIFYSIGNATURES&dID=<$ url( dID ) $><$ include idc_token_url_appending_parameter $>" ) $>
	<$ exec setValue( "PopupProps", "ifClause", "NOT strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), '' )" ) $>
	<$ exec setValue( "PopupProps", "linkTarget", "" ) $>
	<$ exec setValue( "PopupProps", "class", "document" ) $>
	<$ exec setValue( "PopupProps", "id", "reviewSignatures" ) $>
	
	<$ if pageDebug and isTrue( pageDebug ) $>
		<$ trace( traceLog & '\n\t###end extra_setup_search_results_action_popups log###', '#console' ) $>
	<$ endif $>
<@ end @>
<@ dynamichtml custom_docinfo_menus_setup @>
	<$ traceLog = '###begin custom_docinfo_menus_setup log' $>
	<$ include super.custom_docinfo_menus_setup $>

	<$ include cosign_compute_userhasrequiredrole $>
	<$ exec rsAppendNewRow( "PageMenusData" ) $>
	<$ PageMenusData.nodeId = "SIGN_WITH_COSIGN" $>
	<$ PageMenusData.parentId = "CONTENT_ACTIONS" $>
	<$ PageMenusData.label = "wwSignWithCoSign" $>
	<$ PageMenusData.ifClause = "isTrue( userHasRequiredRole ) AND userHasRole( 'CoSignSigner' ) AND ( NOT strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), '' ) AND  NOT strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), 'Externally Signed' ) )" $>
	<$ PageMenusData.type = "item" $>
	<$ PageMenusData.href = "<$ HttpCgiPath $>?IdcService=COSIGN_SIGNDOCUMENT&dDocName=<$ url( dDocName ) $>&dID=<$ url( dID ) $><$ include idc_token_url_appending_parameter $>" $>
	<$ PageMenusData.loadOrder = 10 $>

	<$ exec rsAppendNewRow( "PageMenusData" ) $>
	<$ PageMenusData.nodeId = "REVIEW_SIGNATURES" $>
	<$ PageMenusData.parentId = "CONTENT_ACTIONS" $>
	<$ PageMenusData.label = "wwReviewSignatures" $>
	<$ PageMenusData.ifClause = "NOT strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), '' )" $>
	<$ PageMenusData.type = "item" $>
	<$ PageMenusData.href = "<$ HttpCgiPath $>?IdcService=COSIGN_VERIFYSIGNATURES&dID=<$ url( dID ) $><$ include idc_token_url_appending_parameter $>" $>
	<$ PageMenusData.loadOrder = 10 $>
	<$ if pageDebug and isTrue( pageDebug ) $>
		<$ trace( traceLog & '\n\t###end custom_docinfo_menus_setup log###', '#console' ) $>
	<$ endif $>
<@ end @>
<@ dynamichtml custom_search_results_action_popup @>
	<$ traceLog = '###begin custom_search_results_action_popup log' $>
	<$ include super.custom_search_results_action_popup $>
	<$ traceLog = traceLog & '\n\tTagged: ' & #env.coSignSignatureProfileMetaField $>
	<$ if NOT strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), '' ) $>
		<$ traceLog = traceLog & '\n\tEnable Sign on Search: ' & enableSignOnSearch $>
		<$ if enableSignOnSearch $>
			<$ traceLog = traceLog & '\n\tCoSigner: ' & userHasRole( 'CoSignSigner' ) &
					'\n\tExternally Signed: ' & strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), 'Externally Signed' ) $>
			<$ if userHasRole( 'CoSignSigner' ) AND NOT strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), 'Externally Signed' ) $>
	<tr>
		<td class="xuiPopupLink" nowrap onMouseOver="glowPopupRow( this,'xuiPopupLink_over','xuiPopupLink' )"
				onMouseOut="fadePopupRow( this,'xuiPopupLink' )" onClick="linkItem_onclick( event, this )">
			<a class="menuA_ChildLinkText" href="<$ SearchHttpCgiPath $>?IdcService=COSIGN_SIGNDOCUMENT&<$ include idc_token_url_parameter $>&dDocName=<$ url( dDocName ) $>&dID=<$ url( dID ) $>"
					target="<$ popupLinkTarget $>"><$ lc( "wwSignWithCoSign" ) $></a>
		</td>
	</tr>
			<$ endif $>
		<$ endif $>
	<tr>
		<td class="xuiPopupLink" nowrap onMouseOver="glowPopupRow( this,'xuiPopupLink_over','xuiPopupLink' )"
				onMouseOut="fadePopupRow( this,'xuiPopupLink' )" onClick="linkItem_onclick( event, this )">
			<a class="menuA_ChildLinkText" href="<$ SearchHttpCgiPath $>?IdcService=COSIGN_VERIFYSIGNATURES&<$ include idc_token_url_parameter $>&dID=<$ url( dID ) $>"
					target="<$ popupLinkTarget $>"><$ lc( "wwReviewSignatures" ) $></a>
		</td>
	</tr>
	<$ endif $>
	<$ if pageDebug and isTrue( pageDebug ) $>
		<$ trace( traceLog & '\n\t###end custom_search_results_action_popup log###', '#console' ) $>
	<$ endif $>
<@ end @>
<@ dynamichtml cosign_compute_userhasrequiredrole @>
	<$ if traceLog $>
		<$ delayTrace = 1 $>
		<$ traceLog = traceLog & '\n\t' $>
	<$ else $>
		<$ traceLog = '' $>
	<$ endif $>
	<$ traceLog = traceLog & '###begin cosign_compute_userhasrequiredrole log###' $>

	<$ userHasRequiredRole = 0 $>
	<$ executeService( 'COSIGN_SHOW_MENU' ) $>
	[[% Swapping in new service to handle process due to change in orientation
	 % Now the user''s roles are being compared against the profiles fetched using the content''s tag
	<$ rsMakeFromString( 'Roles', #active.xCoSignRequiredSignatures ) $>
	<$ if rsExists( 'Roles' ) $>
		<$ loop Roles $>
			<$ traceLog = traceLog & '\n\tRole: ' & Roles.row & ' - ' & userHasRole( Roles.row ) $>
			<$ if userHasRole( Roles.row ) $>
				<$ userHasRequiredRole = 1 $>
				<$ break $>
			<$ endif $>
		<$ endloop $>
	<$ else $>
		<$ userHasRequiredRole = 1 $>
	<$ endif $>%]]

	<$ if not delayTrace and ( pageDebug and isTrue( pageDebug ) ) $>
		<$ trace( traceLog & '\n\t###end cosign_compute_userhasrequiredrole log###', '#console' ) $>
	<$ else $>
		<$ traceLog = traceLog & '\n\t###end cosign_compute_userhasrequiredrole log###' $>
	<$ endif $>
<@ end @>
<@ dynamichtml custom_modify_action_popup_data @>
	<$ disabledActionPopupIds = 'signWithCoSign' $>
	<$ rsMakeFromString( 'Roles', #active.xCoSignRequiredSignatures ) $>
	<$ if rsExists( 'Roles' ) $>
		<$ loop Roles $>
			<$ if userHasRole( Roles.row ) $>
				<$ disabledActionPopupIds = '' $>
				<$ break $>
			<$ endif $>
		<$ endloop $>
	<$ else $>
		<$ disabledActionPopupIds = '' $>
	<$ endif $>	
<@ end @>

[[% Navigation extensions %]]
<@ dynamichtml navigation_filter_rset_menu_item @>
<$ include super.navigation_filter_rset_menu_item $>
<$ if not tmpDeleteRow $>
	<$ if flags like "*isCoSignAdmin*" and isFalse( userHasRole( 'CoSignAdmin' ) ) $>
		<$ tmpDeleteRow = 1 $>
	<$ endif $>
<$ endif $>
<@ end @>

[[% Workflow action extensions %]]
<@ dynamichtml setup_workflow_action_popups @>
	<$ include super.setup_workflow_action_popups $>

	<$ exec rsAppendNewRow( "PopupProps" ) $>
	<$ exec setValue( "PopupProps", "label", lc( "wwReviewSignature" ) ) $>
	<$ exec setValue( "PopupProps", "function", "<$ HttpCgiPath $>?IdcService=COSIGN_VERIFYSIGNATURES&dID=<$ url( dID ) $><$ include idc_token_url_appending_parameter $>" ) $>
	<$ exec setValue( "PopupProps", "ifClause", "" ) $>
	<$ exec setValue( "PopupProps", "linkTarget", "" ) $>
	<$ exec setValue( "PopupProps", "class", "document" ) $>
	<$ exec setValue( "PopupProps", "id", "reviewSignature" ) $>
<@ end @>
<@ dynamichtml wf_reviewer_doc_action_links @>
<$ if CoSignApproved $>
	<$ if AllowReview $>
	<span style="white-space:nowrap">
		<$ if WorkflowStep.dWfStepIsSignature and isTrue( WorkflowStep.dWfStepIsSignature ) $>
		[<$ lc( "wwSignAndApprove" ) $>]
		<$ else $>
		[<$ lc( "wwApprove" ) $>]
		<$ endif $>
	&nbsp;</span>
		<$ if not IsStaging $>
	<span style="white-space:nowrap;padding-bottom:3px"> [<$ lc( "wwReject" ) $>]&nbsp;</span>
		<$ endif $>
		<$ if isTrue( IsPreviewRegistered ) $>
	<span style="white-space:nowrap;padding-bottom:3px">[<$ lc( "wwPreview" ) $>]&nbsp;</span>
		<$ endif $>
	<$ endif $>
	<$ if AllowCheckin and ( UserIsAdmin or UserName like dCheckoutUser ) $>
	<span style="white-space:nowrap;padding-bottom:3px">[<$ lc( "wwCheckin" ) $>]</span>
	<span style="white-space:nowrap;padding-bottom:3px">[<$ lc( "wwUndoCheckOut" ) $>]</span>
	<$ endif $>
	<$if AllowCheckout$>
	<span style="white-space:nowrap;padding-bottom:3px">[<$ lc( "wwCheckOut" ) $>]</span>
	<$endif$>
	<span style="white-space:nowrap;padding-bottom:3px">[<$ lc( "wwReviewSignature" ) $>]&nbsp;</span>
<$ else $>
	<$ include super.wf_reviewer_doc_action_links $>
	<$ include wf_reviewer_doc_action_cosign_link $>
<$ endif $>
<@ end @>
<@ dynamichtml wf_reviewer_doc_action_cosign_link @>
	<span style="white-space:nowrap;padding-bottom:3px">
		[<a href="<$ HttpCgiPath $>?IdcService=COSIGN_VERIFYSIGNATURES&dID=<$ url( dID ) $><$ include idc_token_url_appending_parameter $>" class="xuiLinkText1_Sm" style="font-weight:bold;padding-left:1px;padding-right:1px">
			<$ lc( "wwReviewSignature" ) $>
		</a>]&nbsp;
	</span>
<@ end @>
<@ dynamichtml workflow_doc_action_forms @>
	<$ traceLog = '###begin workflow_doc_action_forms log' $>

	<$ if rsExists( 'WfDocuments' ) $><$ value = getValue( 'WfDocuments', #env.coSignSignatureProfileMetaField ) $>
	<$ elseif rsExists( 'DOC_INFO' ) $><$ value = getValue( 'DOC_INFO', #env.coSignSignatureProfileMetaField ) $>
	<$ elseif rsExists( 'WorkflowInQueue' ) $><$ value = getValue( 'WorkflowInQueue', #env.coSignSignatureProfileMetaField ) $>
	<$ else $><$ value = '' $><$ endif $>
	<$ traceLog = traceLog & '\n\tTagged: ' & value & '\n\tWfDocuments: ' & rsExists( 'WfDocuments' ) & '\n\tWorkflowInQueue: ' & rsExists( 'WorkflowInQueue' ) & '\n\tDOC_INFO: ' & rsExists( 'DOC_INFO' ) $>
	<$ include cosign_compute_workflowtypenotreview $>
	<$ if isTrue( workflowTypeNotReview ) AND ( userHasRole( 'CoSignSigner' ) AND ( value AND ( NOT strEquals( value, '' ) AND value OR NOT strEquals( value, 'Externally Signed' ) ) ) ) $>
<form name="Approve_<$ dID $>" method="POST" action="<$ HttpCgiPath $>">
		<$ include idc_token_form_field $>
	<input type="hidden" name="IdcService" value="COSIGN_SIGNDOCUMENT" />
	<input type="hidden" name="RedirectUrl" value="<$ HttpCgiPath $>?IdcService=REVIEW_WORKFLOW_DOC&WSC_Session=&lt;$ url( WSC_Session ) $&gt;&dProjectID=<$ url( dProjectID ) $>&dWfName=<$ url( dWfName ) $>&dDocName=<$ url( dDocName ) $>&dWfStepName=<$ url( dWfStepName ) $>&CoSignApproved=1<$ if inQueueRedirect $>&inQueueRedirect=1<$ endif $>" />
	<input type="hidden" name="dDocName" value="<$ dDocName $>" />
	<input type="hidden" name="dID" value="<$ dID $>" />
</form>
		<$ if IsStaging $>
<form name="ProblemReport_<$ dID $>" method="GET" action="<$ HttpCgiPath $>">
			<$ include idc_token_form_field $>
			<$ if inQueueRedirect $><input type="hidden" name="inQueueRedirect" value="1" /><$ endif $>
	<input type="hidden" name="IdcService" value="GET_DOCUMENT_PROBLEMREPORTS" />
	<input type="hidden" name="dDocName" value="<$ dDocName $>" />
	<input type="hidden" name="dDocTitle" value="<$ url( dDocTitle ) $>" />
	<input type="hidden" name="dID" value="<$ dID $>" />
</form>
		<$ else $>
<form name="Reject_<$ dID $>" method="GET" action="<$ HttpCgiPath $>">
			<$ include idc_token_form_field $>
			<$ if inQueueRedirect $><input type="hidden" name="inQueueRedirect" value="1" /><$ endif $>
	<input type="hidden" name="IdcService" value="WORKFLOW_REJECT_FORM" />
	<input type="hidden" name="dWfName" value="<$ dWfName $>" />
	<input type="hidden" name="dWfStepName" value="<$ dWfStepName $>" />
	<input type="hidden" name="dID" value="<$ dID $>" />
	<input type="hidden" name="dDocName" value="<$ dDocName $>" />
</form>
		<$ endif $>
		<$ if isTrue( IsPreviewRegistered ) $>
<form name="Preview_<$ dID $>" method="GET" action="<$ HttpCgiPath $>" />
	<input type="hidden" name="IdcService" value="GET_PREVIEW_LIST_EX" />
			<$ if inQueueRedirect $><input type="hidden" name="inQueueRedirect" value="1" /><$ endif $>
	<input type="hidden" name="dWfName" value="<$ dWfName $>" />
	<input type="hidden" name="dID" value="<$ dID $>" />
	<input type="hidden" name="dDocName" value="<$ dDocName $>" />
	<input type="hidden" name="IsSimplePreview" value="1" />
</form>
		<$ endif $>
	<$ else $>
		<$ include super.workflow_doc_action_forms $>
	<$ endif $>

	<$ if pageDebug and isTrue( pageDebug ) $>
		<$ trace( traceLog & '\n\t###end workflow_doc_action_forms log###', '#console' ) $>
	<$ endif $>
<@ end @>
<@ dynamichtml cosign_compute_workflowtypenotreview @>
	<$ if traceLog $>
		<$ delayTrace = 1 $>
		<$ traceLog = traceLog & '\n\t' $>
	<$ else $>
		<$ traceLog = '' $>
	<$ endif $>
	<$ traceLog = traceLog & '###begin cosign_compute_workflowtypenotreview log###' $>

	<$ workflowTypeNotReview = 1 $>

	<$ stepType = '' $>
	<$ if rsExists( 'WfDocuments' ) $>
		<$ stepId = getValue( 'WfDocuments', 'dWfCurrentStepID' ) $>
		<$ loop WorkflowSteps $>
			<$ if strEquals( getValue( 'WorkflowSteps', 'dWfStepID' ), stepId ) $>
				<$ stepType = getValue( 'WorkflowSteps', 'dWfStepType' ) $>
				<$ break $>
			<$ endif $>
		<$ endloop $>
		<$ rsFirst( 'WorkflowSteps' ) $>
	<$ elseif rsExists( 'WorkflowInQueue' ) $><$ stepType = getValue( 'WorkflowInQueue', 'dWfStepType' ) $>
	<$ else $><$ stepType = getValue( '#local', 'dWfStepType' ) $><$ endif $>
	<$ traceLog = traceLog & '\n\tWorkflow Step Type: ' & stepType & '\n\tWfDocuments: ' & rsExists( 'WfDocuments' ) & '\n\tWorkflowInQueue: ' & rsExists( 'WorkflowInQueue' ) $>
	<$ if strEquals( stepType, ':R:' ) $>
		<$ workflowTypeNotReview = 0 $>
	<$ endif $>
	<$ traceLog = traceLog & '\n\tWorkflow Type Not Review: ' & workflowTypeNotReview $>

	<$ if not delayTrace and ( pageDebug and isTrue( pageDebug ) ) $>
		<$ trace( traceLog & '\n\t###end cosign_compute_workflowtypenotreview log###', '#console' ) $>
	<$ else $>
		<$ traceLog = traceLog & '\n\t###end cosign_compute_workflowtypenotreview log###' $>
	<$ endif $>
<@ end @>

[[% CoSign Search Results template table includes %]]
<@ dynamichtml cosign_search_result_item_data_setup @>
	<$ exec rsCreateResultSet( "RowData", "row" ) $>
	<$ exec rsAppendNewRow( "RowData" ) $>
	<$ exec rsCreateResultSet( "PopupData", "row" ) $>
	<$ exec rsAppendNewRow( "PopupData" ) $>

	<$ if showDocSelect $>
		<$ exec setValue( "RowData", "select", "<$ include select_searchapi_results_form $>" ) $>
	<$ endif $>

	<$ if CustomViewColumns $>
		<$ loop CustomViewColumns $>
			<$ fieldName = row $>
			<$ tempViewName = "" $>
			<$ fieldIsTree = "" $>
			<$ exec loadSchemaData( "SchemaFieldConfig", fieldName ) $>
			<$ if dIsOptionList and isTrue( dIsOptionList ) and OptionViewKey $>
				<$ tempViewName = OptionViewKey $>
			<$ elseif dOptionListKey and dOptionListKey like "tree://*" and TreeDefinition $>
				<$ fieldIsTree = 1 $>
			<$ endif $>
			<$ if tempViewName $>
				<$ exec setValue( "RowData", fieldName, "<$ getFieldViewDisplayValue( \"" & fieldName & "\", \"" & tempViewName & "\", #active." & fieldName & " ) $>" ) $>
			<$ elseif fieldIsTree $>
				<$ exec setValue( "RowData", fieldName, "<$ getTreeDisplayValue( \"" & fieldName & "\", #active." & fieldName & " ) $>" ) $>
			<$ else $>
				<$ if dType like "Int" $>
					<$ exec setValue( "RowData", fieldName, "<$ formatInteger( #active." & fieldName & " ) $>" ) $>
				<$ elseif dType like "Decimal" $>
					<$ exec setValue( "RowData", fieldName, "<$ formatDecimalForField( #active." & fieldName & ", '" & fieldName & "' ) $>" ) $>
				<$ else $>
					<$ exec setValue( "RowData", fieldName, "<$ #active." & fieldName & " $>" ) $>
				<$ endif $>
			<$ endif $>
			<$ tempViewName = "" $>
			<$ fieldIsTree = "" $>
			<$ exec clearSchemaData( "SchemaFieldConfig", fieldName ) $>
		<$ endloop $>
		<$ include setup_special_column_row_data $>
	<$ else $>
		<$ hrefClassText="class=\"xuiLinkText2_sm\"" $>
		<$ exec setValue( "RowData", "dDocName", "<$ inc( \"searchapi_result_doc_href_start\" ) $><$ #active.dDocName $><$ inc( \"searchapi_result_href_end\" ) $>" ) $>
		<$ if isTrue( #active.ShowDescriptionInHeadlineView ) $>
			<$ exec setValue( "RowData", "dDocTitle", "<$ include searchapi_result_text2 $>" ) $>
		<$ else $>
			<$ exec setValue( "RowData", "dDocTitle", "<$ if dDocTitle $><$ xml( dDocTitle ) $><$ else $><$ if TITLE $><$ xml( TITLE ) $><$ else $><$ lc( \"wwNoTitle\" ) $><$ endif $><$ endif $>" ) $>
		<$ endif $>
		<$ exec setValue( "RowData", "dInDate", "<$ formatDateOnly( #active.dInDate ) $>" ) $>
		<$ exec setValue( "RowData", "dDocAuthor", "<$ #active.dDocAuthor $>" ) $>
		<$ exec setValue( "RowData", "xSignatureStatus", "<$ #active.xSignatureStatus $>" ) $>
		<$ exec setValue( "RowData", "xSigner", "<$ #active.xSigner $>" ) $>
		<$ exec setValue( "RowData", "xSignTime", "<$ #active.xSignTime $>" ) $>
		<$ exec setValue( "RowData", "xSignatureCount", "<$ if ( getValue( '#active', #env.coSignSignatureProfileMetaField ) AND NOT strEquals( getValue( '#active', #env.coSignSignatureProfileMetaField ), '' ) ) AND NOT strEquals( #active.dDocType, 'CoSignSignatureProfile' ) $><$ #active.xSignatureCount $><$ endif $>" ) $>
		<$ exec setValue( "RowData", #env.coSignSignatureProfileMetaField, "<$ getValue( '#active', #env.coSignSignatureProfileMetaField ) $>" ) $>
	<$ endif $>
	<$ if not noInfoColumn $>
		<$ include actions_search_result_column_item_data_setup $>
	<$ endif $>

	<$ if isTraySearch $>
		<$ exec DocLinkTarget = "contentFrame" $>
	<$ else $>
		<$ exec DocLinkTarget = "_self" $>
	<$ endif $>

	<$ if RowData.dDocName $>
		<$ if customTemplateId and not ( customTemplateId like baseTemplateId ) $>
			<$ hrefClassText="class=\"xuiLinkText2_sm\"" $>
			<$ contentLink = utGetValue( "customlisttemplates/" & strLower( customTemplateId ), "contentLink" ) $>
			<$ if contentLink like "webviewable" $>
				<$ RowData.dDocName = "<$ inc( \"searchapi_result_doc_href_start\" ) $><$ #active.dDocName $><$ inc( \"searchapi_result_href_end\" ) $>" $>
			<$ elseif contentLink like "native" $>
				<$ RowData.dDocName = "<$ inc( \"searchapi_result_native_href_start\" ) $><$ #active.dDocName $><$ inc( \"searchapi_result_href_end\" ) $>" $>
			<$ elseif contentLink like "htmlWebviewable" $>
				<$ RowData.dDocName = "<$ if dcShowExportLink() $><$ inc( \"searchapi_result_dchtml_href_start\" ) $><$ else $><$ inc( \"searchapi_result_doc_href_start\" ) $><$ endif $><$ #active.dDocName $><$ inc( \"searchapi_result_href_end\" ) $>" $>
			<$ elseif contentLink like "htmlNative" $>
				<$ RowData.dDocName = "<$ if dcShowExportLink() $><$ inc( \"searchapi_result_dchtml_href_start\" ) $><$ else $><$ inc( \"searchapi_result_native_href_start\" ) $><$ endif $><$ #active.dDocName $><$ inc( \"searchapi_result_href_end\" ) $>" $>
			<$ elseif contentLink like "none" $>
				<$ RowData.dDocName = "<$ dDocName $>" $>
			<$ endif $>
		<$ endif $>
	<$ endif $>
<@ end @>
<@ dynamichtml cosign_search_result_create_item_includes @>
	<$ resultSetClass = "document", resultSetType = "search" $>
	<$ exec inc( "setup_search_results_action_popups" ) $>
	<$ exec inc( "create_action_popup_container_include" ) $>
	<$ exec inc( "create_slim_table_row_include" ) $>
<@ end @>
<@ dynamichtml cosign_table_header @>
	<$ if tableCounter $>
		<$ tableCounter = tableCounter + 1 $>
	<$ else $>
		<$ tableCounter = 0 $>
	<$ endif $>
	<$ columnCounter = 0 $>
	<$ if slimTableCaptionStr and not slimTableCaption $>
		<$ slimTableCaption = "<span class=\"xuiCaption\">" & slimTableCaptionStr & "</span> <br><br style=\"line-height:5px\">" $>
	<$ endif $>

	<$ if slimTableCaption and ( strIndexOf( slimTableCaption, "<caption" ) == 0 ) $>
		<$ captionAlreadyDefined = 1 $>
	<$ endif $>
	<$ if not captionAlreadyDefined $>
		<$ if MSIE and UsingTrays and isRtl $>
			<$ if slimTableCaption $><$ slimTableCaption $><$ endif $>
		<$ else $>
			<$ if slimTableCaption $> <$ slimTableCaption = "<caption>" &  slimTableCaption & "</caption>" $> <$ endif $>
		<$ endif $>
	<$ endif $>
	<table id="table_<$ tableCounter $>" class="<$ if slimTableClass $><$ slimTableClass $><$ else $>xuiListTable<$ endif $>" style="width:<$ if slimTableWidth $><$ slimTableWidth $><$ else $>100%<$ endif $>;<$ if tableStyle $><$ tableStyle $><$ endif $>" cellpadding="<$ if slimTableCellPadding $><$ slimTableCellPadding $><$ else $>0<$ endif $>" cellspacing="<$ if slimTableCellSpacing $><$ slimTableCellSpacing $><$ else $>0<$ endif $>" <$ if tableSummary $>summary="<$ stripXml( lc( tableSummary ) ) $>"<$ endif $>>
	<$ if ( not MSIE or not UsingTrays or not isRtl ) or captionAlreadyDefined $>
		<$ if slimTableCaption $><$ slimTableCaption $><$ endif $>
	<$ endif $>
	 <tr>
	<$ loop ColumnProperties $>
		<$ if columnCounter > 0 $>
	  <td class="xuiListResizeDragCell_Header" scope="col" id="column_<$ tableCounter $>_<$ columnCounter - 1 $>_resizer_column_<$ tableCounter $>_<$ columnCounter $>" <$ if dhtml_compatible_browser $>onmousedown="idc.dom.util.resizeColumns( 'column_<$ tableCounter $>_<$ columnCounter - 1 $>', 'column_<$ tableCounter $>_<$ columnCounter $>', event )" onmouseover="this.style.cursor='e-resize'" onmouseout="this.style.cursor='default'"<$ endif $>><div class="hidden"><$ lc( "wwColumnBreak" ) $></div></td>
		<$ endif $>
		<$ if ColumnProperties.cssHeaderClass $>
			<$ tableHeaderCss = ColumnProperties.cssHeaderClass $>
		<$ elseif slimTableHeaderCellClass $>
			<$ tableHeaderCss = slimTableHeaderCellClass $>
		<$ else $>
			<$ tableHeaderCss = 'xuiListHeaderCell' $>
		<$ endif $>
	   <td id="column_<$ tableCounter $>_<$ columnCounter $>" <$ if ColumnProperties.headerURL $> onclick="location.href='<$ strTrimWs( ColumnProperties.headerURL ) $>'" onmouseover="this.style.cursor='pointer';this.className='<$ tableHeaderCss $> <$ tableHeaderCss $>-hover';" onmouseout= "this.style.cursor='default';this.className='<$ tableHeaderCss $>'"<$ endif $>  class="<$ tableHeaderCss $>" style="width:<$ if ColumnProperties.width $><$ ColumnProperties.width $><$ else $>auto<$ endif $>;" scope="col">
		<$ include slim_table_header_cell $>
	   </td>
		<$ columnCounter = columnCounter + 1 $>
	<$ endloop $>
	  </tr>
	<$ if not doNotCreateTableResultSetStubs $>
		<$ include create_result_set_stubs_for_table_rows $>
	<$ endif $>
	<$ rowCounter = 0 $>
	<$ popupImageCounter = 0 $>
	<$ popupContainerCounter = 0 $>
	<$ slipTableCaption = "" $>
<@ end @>
<@ dynamichtml cosign_search_result_table_header_setup @>
	<$slimResults=1, useThumbnails="", colWidth_sm="55px", colWidth_med="80px", colWidth_lg="120px"$>
	<$if not isTrue(#active.ShowRevisionsColumn)$><$noRevisionsColumn=1$><$endif$>
	<$exec rsCreateResultSet("ColumnProperties", "id,width,headerLabel,rowAlign")$>
	<$if showDocSelect$>
		<$exec rsAppendNewRow("ColumnProperties")$>
		<$ColumnProperties.id = "select"$>
		<$ColumnProperties.width = colWidth_sm$>
		<$ColumnProperties.rowAlign = "center"$>
		<$ColumnProperties.headerLabel = "wwSelect"$>
	<$endif$>
	<$if customTemplateId and not (baseTemplateId like customTemplateId)$>
		<$columnsString = utGetValue("customlisttemplates/" & strLower(customTemplateId), "columns")$>
	<$endif$>
	<$atLeastOneAutoColumn = 0$>
	<$if columnsString and strLength(columnsString) > 0$>
		<$rsMakeFromString("CustomViewColumns", columnsString)$>
		<$loop CustomViewColumns$>
			<$exec rsAppendNewRow("ColumnProperties")$>
			<$ColumnProperties.id = row$>
			<$if (row like "myViewDescription") or (row like "dDocTitle")$>
				<$atLeastOneAutoColumn = 1$>
				<$ColumnProperties.width = "auto"$>
			<$else$>
				<$if CustomViewColumns.#row == CustomViewColumns.#numRows and (not atLeastOneAutoColumn)$>
					<$ColumnProperties.width = "auto"$>
					<$atLeastOneAutoColumn = 1$>
				<$else$>
					<$ColumnProperties.width = colWidth_lg$>
				<$endif$>
			<$endif$>
			<$if strIndexOf(row, "x") == 0$>
				<$exec loadSchemaData("SchemaFieldConfig", row)$>
				<$ColumnProperties.headerLabel = dCaption$>
				<$exec clearSchemaData("SchemaFieldConfig", row)$>
			<$else$>
				<$ColumnProperties.headerLabel = "ww" & row$>
			<$endif$>
			<$ColumnProperties.rowAlign = lineStartAlignment$>
			<$if (strIndexOf(row, "x")) == 0 or (strIndexOf(row, "d")) == 0$>
				<$if rsFindRowPrimary("SearchSortFields", row)$>
					<$fieldName = row$>
					<$ColumnProperties.headerURL = inc("my_view_header_searchapi_url")$>
				<$endif$>
			<$endif$>
		<$endloop$>
		<$if isFalse(atLeastOneAutoColumn)$>
			<$ColumnProperties.width = "auto"$>
		<$endif$>
	<$else$>
		<$exec rsAppendNewRow("ColumnProperties")$>
		<$fieldName = "dDocName"$>
		<$ColumnProperties.id = fieldName$>
		<$ColumnProperties.width = colWidth_lg$>
		<$ColumnProperties.headerLabel = "wwXuiId"$>
		<$ColumnProperties.rowAlign = lineStartAlignment$>
		<$if rsFindRowPrimary("SearchSortFields", fieldName)$>
			<$ColumnProperties.headerURL = inc("my_view_header_searchapi_url")$>
		<$endif$>

		<$exec rsAppendNewRow("ColumnProperties")$>
		<$fieldName = "dDocTitle"$>
		<$ColumnProperties.id = fieldName$>
		<$ColumnProperties.width = "auto"$>
		<$if isTrue(#active.ShowDescriptionInHeadlineView)$>
			<$ColumnProperties.headerLabel = "wwDescription"$>
		<$else$>
			<$ColumnProperties.headerLabel = "wwTitle"$>
		<$endif$>
		<$ColumnProperties.rowAlign = lineStartAlignment$>
		<$if rsFindRowPrimary("SearchSortFields", fieldName)$>
			<$ColumnProperties.headerURL = inc("my_view_header_searchapi_url")$>
		<$endif$>

		<$exec rsAppendNewRow("ColumnProperties")$>
		<$fieldName = "dInDate"$>
		<$ColumnProperties.id = fieldName$>
		<$ColumnProperties.width = colWidth_sm$>
		<$ColumnProperties.headerLabel = "wwXuiDate"$>
		<$ColumnProperties.rowAlign = "center"$>
		<$if rsFindRowPrimary("SearchSortFields", fieldName)$>
			<$ColumnProperties.headerURL = inc("my_view_header_searchapi_url")$>
		<$endif$>

		<$exec rsAppendNewRow("ColumnProperties")$>
		<$fieldName = "dDocAuthor"$>
		<$ColumnProperties.id = fieldName$>
		<$ColumnProperties.width = colWidth_sm$>
		<$ColumnProperties.headerLabel = "wwAuthor"$>
		<$ColumnProperties.rowAlign = "center"$>
		<$if rsFindRowPrimary("SearchSortFields", fieldName)$>
			<$ColumnProperties.headerURL = inc("my_view_header_searchapi_url")$>
		<$endif$>

		<$exec rsAppendNewRow("ColumnProperties")$>
		<$fieldName = "xSignatureStatus"$>
		<$ColumnProperties.id = fieldName$>
		<$ColumnProperties.width = colWidth_sm$>
		<$ColumnProperties.headerLabel = "wwSignatureStatus"$>
		<$ColumnProperties.rowAlign = "center"$>
		<$if rsFindRowPrimary("SearchSortFields", fieldName)$>
			<$ColumnProperties.headerURL = inc("my_view_header_searchapi_url")$>
		<$endif$>

		<$exec rsAppendNewRow("ColumnProperties")$>
		<$fieldName = "xSigner"$>
		<$ColumnProperties.id = fieldName$>
		<$ColumnProperties.width = colWidth_med$>
		<$ColumnProperties.headerLabel = "wwSigner"$>
		<$ColumnProperties.rowAlign = lineStartAlignment$>
		<$if rsFindRowPrimary("SearchSortFields", fieldName)$>
			<$ColumnProperties.headerURL = inc("my_view_header_searchapi_url")$>
		<$endif$>


		<$exec rsAppendNewRow("ColumnProperties")$>
		<$fieldName = "xSignTime"$>
		<$ColumnProperties.id = fieldName$>
		<$ColumnProperties.width = colWidth_lg$>
		<$ColumnProperties.headerLabel = "wwSignTime"$>
		<$ColumnProperties.rowAlign = "center"$>
		<$if rsFindRowPrimary("SearchSortFields", fieldName)$>
			<$ColumnProperties.headerURL = inc("my_view_header_searchapi_url")$>
		<$endif$>

		<$exec rsAppendNewRow("ColumnProperties")$>
		<$fieldName = "xSignatureCount"$>
		<$ColumnProperties.id = fieldName$>
		<$ColumnProperties.width = colWidth_sm$>
		<$ColumnProperties.headerLabel = "wwSignatureCount"$>
		<$ColumnProperties.rowAlign = lineStartAlignment$>
		<$if rsFindRowPrimary("SearchSortFields", fieldName)$>
			<$ColumnProperties.headerURL = inc("my_view_header_searchapi_url")$>
		<$endif$>
	<$endif$>
	<$ if not noInfoColumn $>
		<$ exec rsAppendNewRow( "ColumnProperties" ) $>
		<$ ColumnProperties.id = "actions" $>
		<$ ColumnProperties.width = colWidth_med $>
		<$ ColumnProperties.headerLabel = "wwXuiActions" $>
		<$ ColumnProperties.rowAlign = "center" $>
	<$ endif $>

	[[% Allow profiles to modify the column header. %]]
	<$ loop ColumnProperties $>
		<$ ColumnProperties.headerLabel = getFieldConfigValue( id, "fieldCaption", ColumnProperties.headerLabel ) $>
	<$ endloop $>
	<$ tableSummary = "wwSearchResultsTable" $>
<@ end @>

[[% ================================================================
 % DEPRECATED: opted to use predefined OL
	%
	% This include will be called to show our overrides on a query, update, or checkin
 % page.  This one handles all three custom fields.
 %
 % "fieldName" is the proper variable to address the field.
<@ dynamichtml std_edit_entry @>
	<$ if NOT isProfileCheckIn AND ( fieldName AND ( fieldName like #env.coSignSignatureProfileMetaField ) ) $>
		<$ include cosign_content_profile_field_entry $>
	<$ else $>
		<$ include super.std_edit_entry $>
	<$ endif $>
<@ end @>
 ==================================================================
 % This include will be called to display the #env.coSignSignatureProfileMetaField 
 % select button on the query, update, and checkin pages.
 %]]
<@ dynamichtml cosign_content_profile_field_entry @>
	<$ include super.std_edit_entry $>
	<$ if isQuery and isTrue( isQuery ) $></td><td><$ endif $>
	<$ myQuery = 'dDocType <matches> `CoSignSignatureProfile`' $>
	<input type="hidden" name="p_name" value="" />
	<input type="button" value="<$ lc( "wwSelectTag" ) $>..." onClick="window.open
( '<$ HttpCgiPath $>?IdcService=COSIGN_RESULTS_POPUP&FormName=<$ formName $>&ElementName=<$ fieldName $>&ReturnField=<$ #env.coSignSignatureProfileMetaField $>&QueryText=<$ url( myQuery ) $>','pick','width=900,height=600' )">
<@ end @>

[[% CoSign Profile Page element includes %]]
[[% Set isEditMode on page
 % Set fieldName on call
 % Set fieldId on call
 % Set fieldDefault on call
 %]]
<@ dynamichtml cosign_memo_entry @>
	<$ fieldRows = 2, fieldColumns = 1 $>
	<$ if NOT fieldId $><$ fieldId = fieldName $><$ endif $>
	<$ include compute_std_field_value $>
	<$ include std_memo_entry $>
	<$ exec inc( 'std_field_cleanup' ) $>
<@ end @>
[[% Set isEditMode on page
 % Set fieldName on call
 % Set fieldId on call
 % Set fieldEditWidth on call
 % Set setReadOnly on call
 % Set fieldDisabled on call
 % Set fieldDefault on call
 %]]
<@ dynamichtml cosign_edit_entry @>
	<$ fieldType = 'Text' $>
	<$ if NOT fieldId $><$ fieldId = fieldName $><$ endif $>
	<$ include compute_std_field_value $>
	<$ if fieldValue AND ( setReadOnly AND isTrue( setReadOnly ) ) $>
		<$ fieldReadOnly = 1, setReadOnly = 0 $>
	<$ endif $>
	<$ if NOT fieldEditWidth $><$ fieldEditWidth = '' $><$ endif $>
	<$ inc( 'std_edit_entry' ) $>
	<$ exec inc( 'std_field_cleanup' ) $>
<@ end @>
[[% Override out of box
 % Reason: basically it was never being called?!?
 %]]
<@ dynamichtml std_edit_entry_inputfield_attributes @>
	<$ include super.std_edit_entry_inputfield_attributes $>
	<$ if fieldOnBlur $>onblur="<$ fieldOnBlur $>"<$ endif $>
	<$ if fieldOnChange $>onchange="<$ fieldOnChange $>"<$ endif$>
	<$ if fieldOnFocus $>onfocus="<$ fieldOnFocus $>"<$ endif$>
	<$ if fieldReadOnly $>readonly="readonly"<$ endif $>
	<$ if fieldDisabled $>disabled="disabled"<$ endif $>
	<$ if fieldOnKeyPress $>onkeypress="<$ fieldOnKeyPress $>"<$ endif $>
<@ end @>
<@ dynamichtml cosign_checkbox_entry @>
 <$ checkboxValueStyle = 'infoLabel' $>
	<$ if NOT fieldId $><$ fieldId = fieldName $><$ endif $>
	<$ checkboxValue = getValue( '#active', fieldName ) $>
	<$ if not checkboxValue $><$ checkboxValue = getValue( '#active', fieldId ) $><$ endif $>
	<$ if not checkboxValue $><$ if checked $><$ checked = 1 $><$ else $><$ checked = '' $><$ endif $>
	<$ elseif fieldCheckValue like checkboxValue $><$ checked = 1 $>
	<$ else $><$ checked = '' $><$ endif $>
	<input type="checkbox" id="<$ fieldId $>" name="<$ fieldName $>" value="<$ fieldCheckValue $>" 
	<$ if fieldDisabled $>disabled="disabled" <$ endif $>
 <$ if checkboxStyle $>class="<$ checkboxStyle $>" <$ endif $>
	<$ if checked $>checked <$ endif $>
	<$ if checkboxFieldFunction $>onClick="<$ checkboxFieldFunction $>" <$ endif $>/>
	<$ if fieldCaption $>
	<label for="<$ fieldId $>"><span class="<$ checkboxValueStyle $>"><$ fieldCaption $></span></label>
	<$ endif $>
	<$ exec inc( 'std_field_cleanup' ) $>
<@ end @>
[[% Set isEditMode on page
 % Set fieldName on call
 % Set fieldId on call
 % Set fieldValue on call
 % Set radioFieldCaption on call
 %]]
<@ dynamichtml cosign_radio_entry @>
 <$ radioValueStyle = 'infoLabel' $>
	<$ if NOT fieldId $><$ fieldId = fieldName $><$ endif $>
	<$ radioValue = getValue( '#active', fieldName ) $>
	<$ if not radioValue $><$ radioValue = getValue( '#active', fieldId ) $><$ endif $>
	<$ if fieldValue like radioValue $><$ checked = 1 $>
	<$ elseif radioValue and checked $><$ checked = '' $><$ endif $>
	<input type="radio" id="<$ fieldId $>" name="<$ fieldName $>" value="<$ fieldValue $>" 
 <$ if radioStyle $>class="<$ radioStyle $>" <$ endif $>
	<$ if checked $>checked <$ endif $>
	<$ if radioDisabled $>disabled="disabled" <$ endif $>
	<$ if radioFieldFunction $>onClick="<$ radioFieldFunction $>" <$ endif $>/>
	<$ if radioFieldCaption $>
	<label for="<$ fieldId $>"><span class="<$ radioValueStyle $>"><$ radioFieldCaption $></span></label>
	<$ endif $>
 <$ exec inc( 'std_field_cleanup' ) $>
<@ end @>
[[% Extended out of box
 % Reason: add custom CoSign variables to the cleanup
	%]]
<@ dynamichtml std_field_cleanup @>
 <$ include super.std_field_cleanup $>
	<$ exec ddSetLocalEmpty( 'cosign_field_cleanup_vars' ) $>
<@ end @>

[[% CoSign frame href includes %]]
<@ dynamichtml cosign_signing_frame @>
<iframe height="884" src="<$ include cosign_signdocument_url $>" width="900" frameborder="0" ></iframe>
<@ end @>
<@ dynamichtml determine_wf_review_rendition_url @>
<$ include super.determine_wf_review_rendition_url $>
<$ if CoSignApproved $>
	<$ wfReviewRenditionUrl = inc( 'cosign_signdocument_url' ) $>
<$ endif $>
<@ end @>
<@ dynamichtml cosign_signdocument_url @>
<$ #env.wscServerAddress $><$ if #env.wscServerPort $>:<$ #env.wscServerPort $><$ endif $>/<$ #env.CoSignServerPath $>/UploadDoc.aspx?sessionID=<$ WSC_Session $>
<@ end @>

[[% Profile Management Page table includes %]]
<@ dynamichtml cosign_profile_define_widths @>
	<$ colWidth_sm = '60px', colWidth_med = '105px', colWidth_med2 = '120px', colWidth_lg = '185px' $>
<@ end @>
<@ dynamichtml cosign_setup_profile_table_header @>
	<$ exec rsCreateResultSet( 'ColumnProperties', 'id,width,headerLabel,rowAlign,cssRowClass') $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'name' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_lg ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwCoSignProfile' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>
	<$ exec setValue( 'ColumnProperties', 'cssRowClass', 'xuiDisplayText_Sm_Bold' ) $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'description' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', 'auto' ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwDescription' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'actions' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_med ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwAction' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>
<@ end @>
<@ dynamichtml cosign_setup_profile_table_row @>
	<$ exec setValue( 'RowData', 'name', '<$ SearchResults.dDocTitle $>' ) $>
	<$ exec setValue( 'RowData', 'description', '<$ if SearchResults.xComments $><$ lc( xComments ) $><$ else $><$ endif $>' ) $>
	<$ exec setValue( 'RowData', 'actions', '<$ include cosign_profile_actions $>' ) $>
<@ end @>
<@ dynamichtml cosign_profile_actions @>
 <$ if SearchResults.dID $>
<a href="<$ HttpCgiPath $>?IdcService=COSIGN_MANAGEMENT_FORM&dID=<$ url( SearchResults.dID ) $>&dDocName=<$ url( SearchResults.dDocName ) $>&idcToken=<$ idcToken $>">
	<b><span class=xxsmall><$lc("wwEdit")$></span></b>
</a>
	<$ else $>
<a href="<$ HttpCgiPath $>?IdcService=COSIGN_MANAGEMENT_FORM&idcToken=<$ idcToken $>">
	<b><span class=xxsmall><$lc("wwAdd")$></span></b>
</a>
	<$ endif $>
<@ end @>

[[% Review Signatures Page table includes %]]
<@ dynamichtml cosign_verify_define_widths @>
	<$ colWidth_sm = '60px', colWidth_med = '105px', colWidth_med2 = '120px', colWidth_lg = '185px', colWidth_fill = '17%' $>
<@ end @>
<@ dynamichtml cosign_setup_verify_table_header @>
	<$ exec rsCreateResultSet( 'ColumnProperties', 'id,width,headerLabel,rowAlign,cssRowClass') $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'id' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_sm ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwSID' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>
	<$ exec setValue( 'ColumnProperties', 'cssRowClass', 'xuiDisplayText_Sm_Bold' ) $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'fname' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_sm ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwFieldName' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'status' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_fill ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwStatus' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'time' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_sm ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwSigningTime' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'email' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_med ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwSignerEmail' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'sname' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_sm ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwSignerName' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'reason' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_fill ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwSignReason' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>

	<$ exec rsAppendNewRow( 'ColumnProperties' ) $>
	<$ exec setValue( 'ColumnProperties', 'id', 'estatus' ) $>
	<$ exec setValue( 'ColumnProperties', 'width', colWidth_fill ) $>
	<$ exec setValue( 'ColumnProperties', 'headerLabel', lc( 'wwCertErrorStatus' ) ) $>
	<$ exec setValue( 'ColumnProperties', 'rowAlign', 'left' ) $>
<@ end @>
<@ dynamichtml cosign_setup_verify_table_row @>
	<$ exec setValue( 'RowData', 'id', '<$ SignatureReview.SID $>' ) $>
	<$ exec setValue( 'RowData', 'fname', '<$ SignatureReview.FIELDNAME $>' ) $>
	<$ exec setValue( 'RowData', 'status', '<$ if SignatureReview.STATUS $><$ lc( "wwStatus" & SignatureReview.STATUS ) $><$ endif $>' ) $>
	<$ exec setValue( 'RowData', 'time', '<$ SignatureReview.SIGNINGTIME $>' ) $>
	<$ exec setValue( 'RowData', 'email', '<$ SignatureReview.SIGNEREMAIL $>' ) $>
	<$ exec setValue( 'RowData', 'sname', '<$ SignatureReview.SIGNERNAME $>' ) $>
	<$ exec setValue( 'RowData', 'reason', '<$ SignatureReview.SIGNREASON $>' ) $>
	<$ exec setValue( 'RowData', 'estatus', '<$ if SignatureReview.CERTERRORSTATUS $><$ lc( "wwCertErrorStatus" & SignatureReview.CERTERRORSTATUS ) $><$ endif $>' ) $>
<@ end @>

<@ dynamicdata cosign_set_doevent_for_hourly_event @>
	<$ doEvent = 1 $>
	<$ trace( "Running CoSign doEvent hourly", "#console") $>
<@ end @>

<@ dynamicdata cosign_field_cleanup_vars @>
name
radioStyle
checkboxStyle
setReadOnly
radioFieldFunction
fieldMaxLength
fieldEditWidth
<@ end @>
<@ dynamicdata CoreMenuItems @>
id, label, linkType, linkData
ARX, wwCoSign, ,
COSIGN_LIST, wwCoSignManagement, cgi, IdcService=COSIGN_MANAGEMENT<$ include idc_token_url_appending_parameter $>
COSIGN_ADD, wwCoSignCreation, cgi, IdcService=COSIGN_MANAGEMENT_FORM<$ include idc_token_url_appending_parameter $>
<@ end @>
<@ dynamicdata CoreMenuItemRelationships @>
<?commatable mergeKey="primaryKey"?>
parentId, id, loadOrder
MENU_B, ARX, 1000
ARX, COSIGN_LIST, 10
ARX, COSIGN_ADD, 10
<@ end @>
<@ dynamicdata CoreMenuItemsFlags @>
id, flags
ARX, isCoSignAdmin
<@ end @>
 </body>
</html>